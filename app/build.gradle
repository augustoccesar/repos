plugins {
    id 'application'
    id 'checkstyle'
    id 'org.graalvm.buildtools.native' version '0.10.4'
}

version = "0.5.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'info.picocli:picocli:4.7.6'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'org.apache.commons:commons-compress:1.26.1'

    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.1'
    testImplementation 'org.wiremock:wiremock:3.13.0'
    testImplementation 'ch.qos.logback:logback-classic:1.5.13'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

application {
    mainClass = 'se.augustocesar.repos.CliEntrypoint'
}

run {
    standardInput = System.in
    outputs.upToDateWhen { false }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('install', Copy) {
    dependsOn nativeCompile
    from "build/native/nativeCompile"
    into "${System.env.HOME}/.repos/bin"
    include "repos"
}

def appVersion = project.version
tasks.named('processResources') {
    inputs.property("version", appVersion)

    filesMatching("version.properties") {
        expand(version: appVersion)
    }
}

graalvmNative {
    binaries {
        main {
            imageName = "repos"
            mainClass = "se.augustocesar.repos.CliEntrypoint"
            buildArgs.add("-Os")
            buildArgs.add('--enable-url-protocols=https')
        }
    }
}
